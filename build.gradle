import work.lclpnet.build.task.GithubDeploymentTask

plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id 'gradle-build-utils' version '1.7.0'
}

Properties props = buildUtils.loadProperties('publish.properties')  // will be empty, if the file is missing

version = buildUtils.gitVersion()  // determine project version from the latest git tag on the current branch
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url "https://repo.lclpnet.work/repository/internal"
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "combat-control" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath + main.getOutput() + client.compileClasspath + client.getOutput()
        runtimeClasspath += main.runtimeClasspath + main.getOutput() + client.runtimeClasspath + client.getOutput()
    }
}

loom {
    runs {
        named('server') {
            source 'testmod'
        }

        named('client') {
            source 'testmod'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "work.lclpnet.mods.kibu:kibu-hooks:${project.kibu_hooks_version}"

    include(modImplementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.2.0")))

    include(implementation 'org.json:json:20230227')
    include(implementation('work.lclpnet:json-config4j:1.0.0'))
}

[processResources, processTestmodResources].each {
	it.inputs.property "version", project.version

	it.filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release.set(17)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

[jar, sourcesJar].each {
	it.from("LICENSE.md") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

def env = System.getenv()

tasks.register("github", GithubDeploymentTask) {
    dependsOn tasks.remapJar

    config {
        token = env.get("GITHUB_TOKEN")
        repository = env.get("GITHUB_REPOSITORY")
    }

    release {
        title = "[$project.minecraft_version] $project.name $project.version"
        tag = buildUtils.latestTag()
    }

    assets.add(tasks.remapJar.archiveFile.get())
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.base.archivesName.get()

            from components.java

            pom {
                name = 'combat-control'
                description = 'Player-based combat control mod that aims brings back 1.8 combat to newer versions '
            }
        }
    }

    // automatically use DEPLOY_URL, DEPLOY_USER and DEPLOY_PASSWORD environment variables
    // or mavenHost, mavenUser and mavenPassword from props
    buildUtils.setupPublishRepository(repositories, props)
}